<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Tree_Set" Id="{e403c334-65ae-05d4-1e23-5539d62697d6}" SpecialFunc="None">
    <Declaration><![CDATA[// A collection that contains no duplicate items implemented using an iterative AVL Tree.
// This data structure was implemented with the help of Max Goren's iterative AVL tree in C++. See Function Block's body for more details.
{attribute 'enable_dynamic_creation'}
{attribute 'no_assign'}
{attribute 'no_explicit_call' := 'direct call for this function block is not allowed'}
FUNCTION_BLOCK FB_Tree_Set EXTENDS FB_Collection IMPLEMENTS I_Tree_Set //, I_Enumerable, I_Copyable
VAR
	pRoot : POINTER TO T_AVL_Node;
	Traversal : T_BST_Traversal := T_BST_Traversal.In_Order; 
	pValues : POINTER TO FB_Array_List;
	fbImmutable_Values_List : FB_Immutable_List;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*
	https://github.com/maxgoren
	http://www.maxgcoding.com/iterative-avl-trees-insertion/
	http://www.maxgcoding.com/iterative-avl-trees-deletion/

	MIT License
	
	Copyright (c) 2023 Max Goren
	
	Permission is hereby granted, free of charge, to any person obtaining a copy
	of this software and associated documentation files (the "Software"), to deal
	in the Software without restriction, including without limitation the rights
	to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
	copies of the Software, and to permit persons to whom the Software is
	furnished to do so, subject to the following conditions:
	
	The above copyright notice and this permission notice shall be included in all
	copies or substantial portions of the Software.
	
	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
	IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
	FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
	AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
	LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
	OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
	SOFTWARE.
*)
]]></ST>
    </Implementation>
    <Folder Name="Internal" Id="{9df32180-e72b-072a-1958-7defcfe6909b}" />
    <Property Name="_Traversal" Id="{3a3db1cd-d9e5-0910-1b94-8ffc69d147ef}">
      <Declaration><![CDATA[// Traversal method for retrieving values in the tree set.
PROPERTY _Traversal : T_BST_Traversal]]></Declaration>
      <Get Name="Get" Id="{2da9ed89-f5de-06b6-08bd-447777abd3fa}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Traversal := THIS^.Traversal;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{374922a9-d54b-0892-30dd-a505479b8f11}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[THIS^.Traversal := _Traversal;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="Balance" Id="{e1c43f4a-0780-0618-1a1f-dfa58f3c99da}" FolderPath="Internal\">
      <Declaration><![CDATA[// Performs rotation and updates heights until tree is balanced.
METHOD PROTECTED Balance
VAR_INPUT
    pNode : POINTER TO T_AVL_Node;
END_VAR
VAR 
	pTmp : POINTER TO T_AVL_Node;
    Balance_Factor : T_Position;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[WHILE pNode <> 0 AND_THEN pNode^.pParent <> 0 DO
	pTmp := pNode^.pParent;
	
	THIS^.Update_Height(pTmp);
	
	IF THIS^.Get_Balance(pTmp) < -1 THEN
		IF THIS^.Get_Balance(pTmp^.pRight) > 0 THEN THIS^.Rotate_Right(pTmp^.pRight); END_IF
		THIS^.Rotate_Left(pTmp);
		
	ELSIF THIS^.Get_Balance(pTmp) > 1 THEN
		IF THIS^.Get_Balance(pTmp^.pLeft) < 0 THEN THIS^.Rotate_Left(pTmp^.pLeft); END_IF
		THIS^.Rotate_Right(pTmp);
		END_IF
	
	pNode := pNode^.pParent;
	END_WHILE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Clear" Id="{7bfee972-e71e-0dde-00bc-2686d9c1449b}">
      <Declaration><![CDATA[// Empties/Clears/Deletes every item in the set.
METHOD Clear : I_Set]]></Declaration>
      <Implementation>
        <ST><![CDATA[Clear := THIS^;

THIS^.Clear_Node(THIS^.pRoot);

IF THIS^.pValues <> 0 THEN
	THIS^.fbImmutable_Values_List(ipList := 0);
	__DELETE(THIS^.pValues);
	END_IF

THIS^.pRoot := 0;
THIS^._Count := 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Clear_Node" Id="{e0d018bd-ec22-0b04-2e46-bc385286cd59}" FolderPath="Internal\">
      <Declaration><![CDATA[// Deletes the node and its tree/sub-trees.
{warning disable C0394}
METHOD PROTECTED Clear_Node
VAR_INPUT
	pRoot : POINTER TO T_AVL_Node;
END_VAR
VAR
	fbNode_Stack : FB_Stack;
	pTmp : POINTER TO T_AVL_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pRoot = 0 THEN RETURN; END_IF

fbNode_Stack.Push(pRoot);

WHILE NOT fbNode_Stack._Empty DO
	fbNode_Stack.Get(pTmp);
	fbNode_Stack.Pop();
	
	IF pTmp^.pRight <> 0 THEN fbNode_Stack.Push(pTmp^.pRight); END_IF
	IF pTmp^.pLeft <> 0 THEN fbNode_Stack.Push(pTmp^.pLeft); END_IF
	
	THIS^.Delete_Node(pTmp);
	END_WHILE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Contains" Id="{2ae2e9f1-ec7e-02ec-1f41-36646dca25fd}">
      <Declaration><![CDATA[// Checks if item is contained in set.
METHOD Contains : BOOL
VAR_INPUT
	Item : ANY; // Item to find.
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Contains := THIS^.Search_Node(THIS^.pRoot, Item) <> 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Create_Node" Id="{245993d0-b330-0cd1-3c7f-37cbceaf2636}" FolderPath="Internal\">
      <Declaration><![CDATA[// Creates a new node in dynamic memory with a clone of the generic item as it data.
METHOD PROTECTED Create_Node : POINTER TO ST_AVL_NODE
VAR_INPUT
	Item : T_Generic;
END_VAR
VAR_OUTPUT
	bSuccess : BOOL;
	Error : T_Error;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Create_Node := __NEW(ST_AVL_NODE);

IF Create_Node = 0 THEN Error := GVL_Errors.ERR_MALLOC_FAILED; RETURN; END_IF

Create_Node^.Data := F_Clone_Generic(Item, bSuccess => bSuccess, Error => Error);

IF NOT bSuccess THEN __DELETE(Create_Node); RETURN; END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Delete_Node" Id="{08fe334e-8b10-02c0-2998-1d75606b87b6}" FolderPath="Internal\">
      <Declaration><![CDATA[// Safety deletes node.
// If the node and/or it's value/data is in dynamic memory it/they will be deleted.
METHOD PROTECTED Delete_Node
VAR_INPUT
	pNode : REFERENCE TO POINTER TO T_AVL_Node;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (pNode = 0) THEN RETURN; END_IF 

F_Delete_Generic(pNode^.Data);

CASE Tc2_System.F_CheckMemoryArea(pNode, 1) OF
	Tc2_System.E_TcMemoryArea.Dynamic: __DELETE(pNode);
ELSE
	pNode := 0;
	END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_exit" Id="{53c24bcf-64a8-08a1-34cc-8b4c4cc2abee}">
      <Declaration><![CDATA[{attribute 'hide'}
METHOD FB_exit : BOOL
VAR_INPUT
	bInCopyCode : BOOL; // if TRUE, the exit method is called for exiting an instance that is copied afterwards (online change).
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// If this is an online change or this collection is empty then ignore. 
IF bInCopyCode THEN RETURN; END_IF

THIS^.Clear();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Find_Minimum_Node" Id="{fba23543-b5ad-020a-0e53-8d9b3d35431c}" FolderPath="Internal\">
      <Declaration><![CDATA[// Finds the node with the smallest item in a tree or sub-tree.
METHOD PROTECTED Find_Minimum_Node : POINTER TO T_AVL_Node
VAR_INPUT
	pNode : POINTER TO T_AVL_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pNode = 0 THEN RETURN; END_IF

WHILE pNode^.pLeft <> 0 DO
	pNode := pNode^.pLeft;
	END_WHILE
	
Find_Minimum_Node := pNode;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Get_Balance" Id="{95e61307-7d93-01a6-3c09-46aac7ea0f1e}" FolderPath="Internal\">
      <Declaration><![CDATA[// Gets the difference between the left and right sub-trees.
METHOD PROTECTED Get_Balance : T_Position
VAR_INPUT
	pNode : POINTER TO T_AVL_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Get_Balance :=  THIS^.Get_Height(pNode^.pLeft) - THIS^.Get_Height(pNode^.pRight);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Get_Height" Id="{ea2b4cb7-7f36-0409-162c-ba5f657fb51a}" FolderPath="Internal\">
      <Declaration><![CDATA[// Safely gets the height of a sub-tree.
METHOD PROTECTED Get_Height : T_Position
VAR_INPUT
	pNode : POINTER TO T_AVL_Node;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Get_Height := SEL(pNode <> 0, -1, pNode^.Height);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Get_Values" Id="{26ec2bbf-c3ac-0c05-11be-6442e6bf45cb}">
      <Declaration><![CDATA[// Gets an immutable list of the values in the set.
{warning disable C0394}
METHOD Get_Values : I_Immutable_List
VAR
	pCurrent, pPrevious, pTmp : POINTER TO T_AVL_Node := THIS^.pRoot;
	fbNode_Buffer : FB_Queue;
	bSuccess : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF THIS^.pValues = 0 THEN THIS^.pValues := __NEW(FB_Array_List(Initial_Capacity := 0)); END_IF
THIS^.fbImmutable_Values_List(ipList := THIS^.pValues^);
Get_Values := THIS^.fbImmutable_Values_List;

THIS^.pValues^.Clear();

IF pCurrent = 0 THEN RETURN; END_IF

WHILE (NOT fbNode_Buffer._Empty) OR_ELSE pCurrent <> 0 DO
	
	CASE THIS^._Traversal OF
		T_BST_Traversal.In_Order:
			IF pCurrent <> 0 THEN 
				fbNode_Buffer.Enqueue_At_Front(pCurrent);
				pCurrent := pCurrent^.pLeft;
			ELSE
				fbNode_Buffer.Get(pCurrent).Dequeue();
				THIS^.pValues^.Insert_Generic(THIS^.pValues^._Count, pCurrent^.Data);
				pCurrent := pCurrent^.pRight;
				END_IF
				
		T_BST_Traversal.Pre_Order:
			IF pCurrent <> 0 THEN 
				THIS^.pValues^.Insert_Generic(THIS^.pValues^._Count, pCurrent^.Data);
				IF pCurrent^.pRight <> 0 THEN fbNode_Buffer.Enqueue_At_Front(pCurrent^.pRight); END_IF
				pCurrent := pCurrent^.pLeft;
			ELSE
				fbNode_Buffer.Get(pCurrent).Dequeue();
				END_IF
			
		T_BST_Traversal.Post_Order:
			WHILE pCurrent <> 0 DO 
				fbNode_Buffer.Enqueue_At_Front(pCurrent);
				pCurrent := pCurrent^.pLeft;
				END_WHILE
				
			IF NOT fbNode_Buffer._Empty THEN
				fbNode_Buffer.Get(pTmp);
				pTmp := pTmp^.pRight;
				IF pTmp = 0 OR_ELSE pTmp = pPrevious THEN 
					fbNode_Buffer.Get(pCurrent).Dequeue();
					THIS^.pValues^.Insert_Generic(THIS^.pValues^._Count, pCurrent^.Data);
					pPrevious := pCurrent;
					pCurrent := 0;
				ELSE
					pCurrent := pTmp;
					END_IF
				END_IF
			
		T_BST_Traversal.Level_Order:
			THIS^.pValues^.Insert_Generic(THIS^.pValues^._Count, pCurrent^.Data);
			IF pCurrent^.pLeft <> 0 THEN fbNode_Buffer.Enqueue(pCurrent^.pLeft); END_IF
			IF pCurrent^.pRight <> 0 THEN fbNode_Buffer.Enqueue(pCurrent^.pRight); END_IF
			fbNode_Buffer.Get(pCurrent).Dequeue(bSuccess => bSuccess);
			IF NOT bSuccess THEN pCurrent := 0; END_IF
	ELSE
		RETURN;
		END_CASE
	END_WHILE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Insert" Id="{e111174b-6aa3-0457-0ad2-38df956adc5f}">
      <Declaration><![CDATA[// Insert item into set.
METHOD Insert 	: I_Set
VAR_INPUT
	Item : ANY; // Data to insert
END_VAR
VAR_OUTPUT
	bSuccess : BOOL; // Indicates whether operation was successful. Success = TRUE, Failure = FALSE
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Insert := THIS^.Insert_Generic(Item, bSuccess => bSuccess);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Insert_Generic" Id="{02180576-d437-0550-321f-a71f08b0c985}">
      <Declaration><![CDATA[// Adds new generic item at into set.
// The set will store a copy of the generic item so as to not modify the original value outside it's scope.
METHOD Insert_Generic : I_Set
VAR_INPUT
	Item : T_Generic; // Item to store
END_VAR
VAR_OUTPUT
	bSuccess : BOOL; // Indicates whether operation was successful. Success = TRUE, Failure = FALSE
END_VAR
VAR
	pCurrent, pPrevious : POINTER TO T_AVL_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Insert_Generic := THIS^;

pCurrent := THIS^.pRoot;
pPrevious := pCurrent;

WHILE pCurrent <> 0 DO
	pPrevious := pCurrent;
	CASE F_Compare_Generics(Item, pCurrent^.Data) OF
		T_Comparison.Less 	 : pCurrent := pCurrent^.pLeft;
		T_Comparison.Greater : pCurrent := pCurrent^.pRight;
		T_Comparison.Equal 	 : RETURN;
	ELSE
		THIS^._Error_Status := GVL_Errors.ERR_INVALID_COMPARISON;
		RETURN;
		END_CASE
	END_WHILE

pCurrent := THIS^.Create_Node(Item, bSuccess => bSuccess, Error => THIS^.Error);

IF NOT bSuccess THEN RETURN; END_IF

IF pPrevious = 0 THEN
	THIS^.pRoot := pCurrent;
ELSE
	CASE F_Compare_Generics(Item, pPrevious^.Data) OF
		T_Comparison.Less: pPrevious^.pLeft := pCurrent;
	ELSE
		pPrevious^.pRight := pCurrent;
		END_CASE
	END_IF

pCurrent^.pParent := pPrevious;
	
THIS^.Balance(pCurrent);
THIS^.Increment_Count();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Move_Node" Id="{2aaa8daf-e951-0ffd-1650-bf0b29729a4f}" FolderPath="Internal\">
      <Declaration><![CDATA[// Moves from node at source into the position of the destination.
METHOD PROTECTED Move_Node
VAR_INPUT
	pDestination, pSource : POINTER TO T_AVL_Node;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pDestination = 0 OR_ELSE pSource = 0 THEN RETURN; END_IF 

IF pDestination^.pParent = 0 THEN
	THIS^.pRoot := pSource;
ELSIF pDestination = pDestination^.pParent^.pLeft THEN
	pDestination^.pParent^.pLeft := pSource;
ELSE
	pDestination^.pParent^.pRight := pSource;
	END_IF
	
IF pSource <> 0 THEN
	pSource^.pParent := pDestination^.pParent;
	END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Remove" Id="{3b265e11-fea4-01d3-19f1-395c72044480}">
      <Declaration><![CDATA[// Removes item from the set.
METHOD Remove : I_Set
VAR_INPUT
	Item : ANY; // Data to remove
END_VAR
VAR_OUTPUT
	bSuccess : BOOL; // Indicates whether operation was successful. Success = TRUE, Failure = FALSE
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Remove := THIS^.Remove_Generic(Item, bSuccess => bSuccess);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Remove_Generic" Id="{f241bea8-792e-0a92-136a-19caf4b69000}">
      <Declaration><![CDATA[// Removes generic item from the set.
METHOD Remove_Generic : I_Set
VAR_INPUT
	Item : T_Generic; // Data to remove
END_VAR
VAR_OUTPUT
	bSuccess : BOOL; // Indicates whether operation was successful. Success = TRUE, Failure = FALSE
END_VAR
VAR
	pTmp : POINTER TO T_AVL_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Remove_Generic := THIS^;

pTmp := THIS^.Search_Node(THIS^.pRoot, Item);

IF pTmp = 0 THEN RETURN; END_IF

THIS^.Balance(THIS^.Remove_Node(pTmp));

THIS^.Decrement_Count();
THIS^.Clear_Error();

IF THIS^._Empty THEN THIS^.pRoot := 0; END_IF

bSuccess := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Remove_Node" Id="{f8c0b73d-df0b-01cc-0943-45282e56f537}" FolderPath="Internal\">
      <Declaration><![CDATA[// Finds and removes a node from a tree/sub-tree and returns the new root of the tree/subtree.
METHOD PROTECTED Remove_Node : POINTER TO T_AVL_Node
VAR_INPUT
	pNode : REFERENCE TO POINTER TO T_AVL_Node;
END_VAR
VAR
	pSuccessor : POINTER TO T_AVL_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (NOT __ISVALIDREF(pNode)) OR_ELSE pNode = 0 THEN RETURN; END_IF 

IF pNode^.pLeft = 0 THEN
	THIS^.Move_Node(pNode, pNode^.pRight);
	Remove_Node := pNode^.pRight;
ELSIF pNode^.pRight = 0 THEN 
	THIS^.Move_Node(pNode, pNode^.pLeft);
	Remove_Node := pNode^.pLeft;
ELSE
	Remove_Node := pSuccessor := THIS^.Find_Minimum_Node(pNode^.pRight);
	IF pSuccessor^.pParent <> pNode THEN 
		THIS^.Move_Node(pSuccessor, pSuccessor^.pRight);
		pSuccessor^.pRight := pNode^.pRight;
		pSuccessor^.pRight^.pParent := pSuccessor;
		END_IF
	THIS^.Move_Node(pNode, pSuccessor);
	pSuccessor^.pLeft := pNode^.pLeft;
	pSuccessor^.pLeft^.pParent := pSuccessor;
	END_IF

THIS^.Delete_Node(pNode);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Rotate_Left" Id="{5e628c58-cfc1-00a9-25f1-c245de014963}" FolderPath="Internal\">
      <Declaration><![CDATA[// Rotates a tree/sub-tree left.
METHOD PROTECTED Rotate_Left
VAR_INPUT
    pX : POINTER TO T_AVL_Node;
END_VAR
VAR
    pY : POINTER TO T_AVL_Node := pX^.pRight;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pY <> 0 THEN
	pX^.pRight := pY^.pLeft;
	IF pY^.pLeft <> 0 THEN pY^.pLeft^.pParent := pX; END_IF
	pY^.pParent := pX^.pParent;
	END_IF
	
IF pX^.pParent = 0 THEN
	THIS^.pRoot := pY;
ELSIF pX = pX^.pParent^.pLeft THEN
	pX^.pParent^.pLeft := pY;
ELSE
	pX^.pParent^.pRight := pY;
	END_IF
	
IF pY <> 0 THEN pY^.pLeft := pX; END_IF
	
pX^.pParent := pY;
THIS^.Update_Height(pX);
THIS^.Update_Height(pY);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Rotate_Right" Id="{1978ee84-b5c2-0edf-08ae-c61d1059739c}" FolderPath="Internal\">
      <Declaration><![CDATA[// Rotates a tree/sub-tree right.
METHOD PROTECTED Rotate_Right
VAR_INPUT
    pX : POINTER TO T_AVL_Node;
END_VAR
VAR
    pY : POINTER TO T_AVL_Node := pX^.pLeft;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pY <> 0 THEN
	pX^.pLeft := pY^.pRight;
	IF pY^.pRight <> 0 THEN pY^.pRight^.pParent := pX; END_IF
	pY^.pParent := pX^.pParent;
	END_IF
	
IF pX^.pParent = 0 THEN
	THIS^.pRoot := pY;
ELSIF pX = pX^.pParent^.pLeft THEN
	pX^.pParent^.pLeft := pY;
ELSE
	pX^.pParent^.pRight := pY;
	END_IF
	
IF pY <> 0 THEN pY^.pRight := pX; END_IF
	
pX^.pParent := pY;
THIS^.Update_Height(pX);
THIS^.Update_Height(pY);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Search_Node" Id="{c761d834-a9bd-008b-229f-f128ebb26f53}" FolderPath="Internal\">
      <Declaration><![CDATA[// Checks if an item is in a tree/ sub-tree.
METHOD PROTECTED Search_Node : POINTER TO T_AVL_Node
VAR_INPUT
	pNode : POINTER TO T_AVL_Node;
	Item : T_Generic;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[WHILE pNode <> 0 DO
	CASE F_Compare_Generics(Item, pNode^.Data) OF
		T_Comparison.Less	: pNode := pNode^.pLeft;
		T_Comparison.Greater: pNode := pNode^.pRight; 
		T_Comparison.Equal	: 
			Search_Node := pNode;
			RETURN;
		END_CASE
	END_WHILE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Update_Height" Id="{ee8a3416-a884-083e-0760-862cc2e521e2}" FolderPath="Internal\">
      <Declaration><![CDATA[METHOD PROTECTED Update_Height
VAR_INPUT
	pNode : POINTER TO T_AVL_Node;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pNode^.Height := 1 + MAX(THIS^.Get_Height(pNode^.pLeft), THIS^.Get_Height(pNode^.pRight));]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>