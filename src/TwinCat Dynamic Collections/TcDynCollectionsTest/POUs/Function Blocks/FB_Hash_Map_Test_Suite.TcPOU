<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Hash_Map_Test_Suite" Id="{f3d5ec08-9c7d-09d8-1fd0-35a06a0cff90}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Hash_Map_Test_Suite EXTENDS TcUnit.FB_TestSuite
VAR
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[Check_If_Clear_Empties_Items();
Check_If_Insert_Adds_Items();
Check_If_Update_Updates_Items();
Check_If_Upsert_Updates_Items_Or_Adds_If_They_Dont_Exist();
Check_If_Remove_Removes_Items();
Check_If_Contains_Returns_True_If_Key_In_Map();
Check_If_Get_Key_Values_Returns_Keys_Values();]]></ST>
    </Implementation>
    <Method Name="Check_If_Clear_Empties_Items" Id="{b94c7f78-563c-007f-2570-243846459a2c}">
      <Declaration><![CDATA[METHOD Check_If_Clear_Empties_Items
VAR_INST
	fbMap : FB_Hash_Map(0);
	sKey : STRING := 'I love cats';
	nValue : INT := 42;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('Check_If_Clear_Empties');
	fbMap
		.Insert(sKey, nValue)
		.Clear();
		
	THIS^.AssertEquals_DINT(0, fbMap._Count, 'Map was not empty after inserting data.');
	
	TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Check_If_Contains_Returns_True_If_Key_In_Map" Id="{17118c16-706f-0cd0-38f5-2463dd316e28}">
      <Declaration><![CDATA[METHOD Check_If_Contains_Returns_True_If_Key_In_Map
VAR_INST
	fbMap : FB_Hash_Map(0);
	sItem : STRING := 'I love cats';
	nItem : INT := 42;
	bResult : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('Check_If_Contains_Returns_True_If_Key_In_Map');

	bResult := fbMap
		.Insert(sItem, nItem)
		.Insert(nItem, sItem)
		.Contains(nItem);
		
	THIS^.AssertEquals_BOOL(TRUE, bResult, 'Failed to check if item is in map.');

	TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Check_If_Get_Key_Values_Returns_Keys_Values" Id="{8409f406-e3cf-013d-11f0-9a017b9746b0}">
      <Declaration><![CDATA[METHOD Check_If_Get_Key_Values_Returns_Keys_Values
VAR_INST
	fbMap : FB_Hash_Map(0);
	arnItems : ARRAY[0..3] OF DINT := [1,2,3,4];
	arnReturned_Keys,
	arnReturned_Values : ARRAY[0..3] OF DINT;
	arIndex : ARRAY[0..3] OF T_Position := [4(-1)];
	i : DINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('Check_If_Get_Key_Values_Returns_Keys_Values');

	FOR i := 0 TO 3 DO
    	fbMap.Insert(arnItems[i], arnItems[i]);
    	END_FOR

	fbMap.Get_Keys()
		.Search(arnItems[0], index => arIndex[0])
		.Search(arnItems[1], index => arIndex[1])
		.Search(arnItems[2], index => arIndex[2])
		.Search(arnItems[3], index => arIndex[3]);
	fbMap.Get_Keys()
		.Get(arIndex[0], arnReturned_Keys[0])
		.Get(arIndex[1], arnReturned_Keys[1])
		.Get(arIndex[2], arnReturned_Keys[2])
		.Get(arIndex[3], arnReturned_Keys[3]);
	THIS^.AssertEquals_DINT(1, arnReturned_Keys[0],'');
	THIS^.AssertEquals_DINT(2, arnReturned_Keys[1],'');
	THIS^.AssertEquals_DINT(3, arnReturned_Keys[2],'');
	THIS^.AssertEquals_DINT(4, arnReturned_Keys[3],'');
	
	fbMap.Get_Values()
		.Get(arIndex[0], arnReturned_Values[0])
		.Get(arIndex[1], arnReturned_Values[1]);
	fbMap.Get_Values()
		.Get(arIndex[2], arnReturned_Values[2])
		.Get(arIndex[3], arnReturned_Values[3]);
	THIS^.AssertEquals_DINT(1, arnReturned_Values[0],'');
	THIS^.AssertEquals_DINT(2, arnReturned_Values[1],'');
	THIS^.AssertEquals_DINT(3, arnReturned_Values[2],'');
	THIS^.AssertEquals_DINT(4, arnReturned_Values[3],'');
	
	TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Check_If_Insert_Adds_Items" Id="{ef5de521-39c1-0b5b-3edc-ab7114618502}">
      <Declaration><![CDATA[METHOD Check_If_Insert_Adds_Items
VAR_INST
	fbMap : FB_Hash_Map(0);
	sItem : STRING := 'I love cats';
	nItem : INT := 42;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('Check_If_Insert_Adds_Items');

	fbMap
		.Insert(sItem, nItem)
		.Insert(nItem, sItem);
		
	THIS^.AssertEquals_DINT(2, fbMap._Count, 'Failed to insert items into map.');
	
	TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Check_If_Remove_Removes_Items" Id="{3e3b7638-7ce1-0d2f-1733-54ae117f8986}">
      <Declaration><![CDATA[METHOD Check_If_Remove_Removes_Items
VAR_INST
	fbMap : FB_Hash_Map(0);
	sItem : STRING := 'I love cats';
	nItem : INT := 42;
	fItem : LREAL := 37.4;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('Check_If_Remove_Removes_Items');

	fbMap
		.Insert(sItem, nItem)
		.Insert(nItem, fItem)
		.Insert(fItem, sItem)
		.Remove(nItem);
		
	THIS^.AssertEquals_DINT(2, fbMap._Count, 'Failed to remove item in set.');
	THIS^.AssertEquals_BOOL(TRUE, fbMap.Contains(sItem), 'Failed to remove item in set.');
	THIS^.AssertEquals_BOOL(FALSE, fbMap.Contains(nItem), 'Failed to remove item in set.');
	THIS^.AssertEquals_BOOL(TRUE, fbMap.Contains(fItem), 'Failed to remove item in set.');	
	
	TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Check_If_Update_Updates_Items" Id="{debebe10-89ee-0885-2289-376bc9a94f27}">
      <Declaration><![CDATA[METHOD Check_If_Update_Updates_Items
VAR_INST
	fbMap : FB_Hash_Map(0);
	sItem : STRING := 'I love cats';
	nItem : INT := 42;
	fItem : LREAL := 37.4;
	fNew_Item : LREAL := 3.14;
	fR_Item,
	fR_Updated_Item : LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('Check_If_Update_Updates_Items');
	fbMap
		.Insert(sItem, nItem)
		.Insert(nItem, fItem)
		.Insert(fItem, sItem)
		.Get(nItem, fR_Item)
		.Update(nItem, fNew_Item)
		.Get(nItem, fR_Updated_Item);
	
	THIS^.AssertEquals_LREAL(37.4, fR_Item, 0.0001, 'Failed to get value to be updated in map.');
	THIS^.AssertEquals_LREAL(3.14, fR_Updated_Item, 0.0001, 'Failed to get updated value in map.');
		
	TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Check_If_Upsert_Updates_Items_Or_Adds_If_They_Dont_Exist" Id="{c7dae87b-4561-0fed-3c5f-567917592828}">
      <Declaration><![CDATA[METHOD Check_If_Upsert_Updates_Items_Or_Adds_If_They_Dont_Exist
VAR_INST
	fbMap : FB_Hash_Map(0);
	nItem : INT := 42;
	fItem : LREAL := 37.4;
	fNew_Item : LREAL := 3.14;
	fR_Item,
	fR_Updated_Item : LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[TcUnit.TEST('Check_If_Upsert_Updates_Items_Or_Adds_If_They_Dont_Exist');
	fbMap
		.Upsert(nItem, fItem)
		.Get(nItem, fR_Item)
		.Upsert(nItem, fNew_Item)
		.Get(nItem, fR_Updated_Item);
		
	THIS^.AssertEquals_LREAL(37.4, fR_Item, 0.0001, 'Failed to insert key value in map.');
	THIS^.AssertEquals_LREAL(3.14, fR_Updated_Item, 0.0001, 'Failed to update value in map.');
	TEST_FINISHED();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>